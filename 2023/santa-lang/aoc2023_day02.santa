input: read("aoc://2023/2")

let parse_required_cubes_per_game = lines >> map |line| {
    let parse_colour = |colour, line| line |> regex_match_all("\\d+ " + colour) |> map(split(" ") >> first >> int)
    #{
        "id": line |> regex_match("Game (\\d+)") |> first |> int,
        "red": line |> parse_colour("red") |> max,
        "green": line |> parse_colour("green") |> max,
        "blue": line |> parse_colour("blue") |> max,
    }
}

part_one: {
    parse_required_cubes_per_game(input)
        |> filter_map |game| {
            if game["red"] <= 12 && game["green"] <= 13 && game["blue"] <= 14 {
                game["id"]
            }
        }
        |> sum
}

part_two: {
    parse_required_cubes_per_game(input)
        |> map(|game| game["red"] * game["green"] * game["blue"])
        |> sum
}

test: {
    input: "Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green"
    part_one: 8
    part_two: 2286
}

test: {
    input: read("aoc://2023/2")
    part_one: 3035
    part_two: 66027
}
