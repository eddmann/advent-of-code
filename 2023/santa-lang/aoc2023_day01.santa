input: read("aoc://2023/1")

part_one: {
  let calibrate = |value| {
    let digits = regex_match_all("\\d", value)
    int(digits[0] + digits[-1])
  }

  input
    |> lines
    |> map(calibrate)
    |> sum
}

part_two: {
  let to_digit = |number| match number {
    "one" { 1 }
    "two" { 2 }
    "three" { 3 }
    "four" { 4 }
    "five" { 5 }
    "six" { 6 }
    "seven" { 7 }
    "eight" { 8 }
    "nine" { 9 }
    _ { int(number) }
  }

  let calibrate = |value| {
    let start = value |> regex_match("(\\d|one|two|three|four|five|six|seven|eight|nine)") |> first
    let end = value |> reverse |> regex_match("(\\d|enin|thgie|neves|xis|evif|ruof|eerht|owt|eno)") |> first |> reverse
    to_digit(start) * 10 + to_digit(end)
  }

  input
    |> lines
    |> map(calibrate)
    |> sum
}

test: {
  input: "1abc2
pqr3stu8vwx
a1b2c3d4e5f
treb7uchet"
  part_one: 142
}

test: {
  input: "two1nine
eightwothree
abcone2threexyz
xtwone3four
4nineeightseven2
zoneight234
7pqrstsixteen"
  part_two: 281
}

test: {
  input: read("aoc://2023/1")
  part_one: 55017
  part_two: 53539
}
