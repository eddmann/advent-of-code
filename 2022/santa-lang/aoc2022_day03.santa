input: read("aoc://2022/3")

let priorities = #{
  "a": 1,  "b": 2,  "c": 3,  "d": 4,  "e": 5,  "f": 6,  "g": 7,  "h": 8,
  "i": 9,  "j": 10, "k": 11, "l": 12, "m": 13, "n": 14, "o": 15, "p": 16,
  "q": 17, "r": 18, "s": 19, "t": 20, "u": 21, "v": 22, "w": 23, "x": 24,
  "y": 25, "z": 26,

  "A": 27, "B": 28, "C": 29, "D": 30, "E": 31, "F": 32, "G": 33, "H": 34,
  "I": 35, "J": 36, "K": 37, "L": 38, "M": 39, "N": 40, "O": 41, "P": 42,
  "Q": 43, "R": 44, "S": 45, "T": 46, "U": 47, "V": 48, "W": 49, "X": 50,
  "Y": 51, "Z": 52,
}

let intersection = |a, b| {
  reduce(
    |acc, el| if a `contains` el { acc + {el} } else { acc },
    {},
    b
  );
}

let parse_groups = lines >> map(split(""));

let common_item = |groups| {
  reduce(intersection, groups[0], groups[1..])
    |> list
    |> first;
}

part_one: {
  let compartments = |rucksack| {
    let length = size(rucksack) / 2;
    [rucksack[0..length], rucksack[length..]];
  }

  parse_groups(input)
    |> map(compartments >> common_item >> get(_, priorities))
    |> sum;
}

part_two: {
  parse_groups(input)
    |> map(set)
    |> chunk(3)
    |> map(common_item >> get(_, priorities))
    |> sum;
}

test: {
  input: "vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw"
  part_one: 157
  part_two: 70
}

test: {
  input: read("aoc://2022/3")
  part_one: 8515
  part_two: 2434
}
