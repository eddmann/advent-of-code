input: read("aoc://2022/25")

let snafu_to_digit = |snafu| {
  if snafu == nil { return 0; }
  if snafu == "-" { return -1; }
  if snafu == "=" { return -2; }
  return int(snafu);
}

let digit_to_snafu = |digit| {
  if digit == -1 { return "-"; }
  if digit == -2 { return "="; }
  return "" + digit;
}

let snafu_sum = |a, b| {
  let sum_ = |a, b, acc, carry| {
    if a == "" && b == "" && carry == 0 {
      return acc;
    }

    let place = carry + snafu_to_digit(a[-1]) + snafu_to_digit(b[-1]);
    let next_carry = if place > 2 { 1 } else { if place < -2 { -1 } else { 0 } };
    let digit = ((5 + place + 2) % 5) - 2;

    sum_(a[0..-1], b[0..-1], digit_to_snafu(digit) + acc, next_carry);
  };

  sum_(a, b, "", 0);
}

part_one: {
  let snafu = lines(input);
  snafu[1..] |> fold(snafu[0], snafu_sum);
}

test: {
  input: "1=-0-2
12111
2=0=
21
2=01
111
20012
112
1=-1=
1-12
12
1=
122"

  part_one: "2=-1=0"
}

test: {
  input: read("aoc://2022/25")
  part_one: "2-0==21--=0==2201==2"
}
