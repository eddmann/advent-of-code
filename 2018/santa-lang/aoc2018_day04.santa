input: read("aoc2018_day04.input")

let sleep_or_default = |sleep, guard| {
  if sleep[guard] { sleep[guard] } else { zip(0..59, [0] * 60) };
};

let tally_guards_sleep = |input| {
  input
    |> lines
    |> sort(>)
    |> reduce_s(
      |[sleep, guard, start], action| {
        if action[25] == "#" {
          return [sleep, int((action |> split(" "))[3][1..]), nil];
        }

        if action[25] == "a" {
          return [sleep, guard, int(action[15..17])];
        }

        let sleep_range = start..int(action[15..17]) - 1;

        let updated_sleep = sleep_or_default(sleep, guard)
          |> map(|[minute, total]| {
            [minute, if sleep_range `contains` minute { total + 1 } else { total }]
          });
        
        return [sleep + #{guard: updated_sleep}, guard, nil];
      },
      [#{}, nil, nil]
    );
};

part_one: {
  let guards_sleep = tally_guards_sleep(input);

  let guards = guards_sleep |> reduce(
    |acc, v, k| acc + [[k, v |> reduce(|acc, [_, t]| acc + t, 0)]],
    []
  );

  let id = (guards |> sort(|a, b| a[1] < b[1]))[0][0];
  let minute = (guards_sleep[id] |> sort(|a, b| { a[1] < b[1] }))[0][0];
  
  id * minute;
}

part_two: {
  let guards_sleep = tally_guards_sleep(input);

  let guards = guards_sleep |> reduce(
    |acc, v, k| acc + [[k, v |> reduce(|max, [_, t]| if max < t { t } else { max }, 0)]],
    []
  );

  let id = (guards |> sort(|a, b| a[1] < b[1]))[0][0];
  let minute = (guards_sleep[id] |> sort(|a, b| { a[1] < b[1] }))[0][0];
  
  id * minute;
}

test: {
  input: "[1518-11-01 00:00] Guard #10 begins shift
[1518-11-01 00:05] falls asleep
[1518-11-01 00:25] wakes up
[1518-11-01 00:30] falls asleep
[1518-11-01 00:55] wakes up
[1518-11-01 23:58] Guard #99 begins shift
[1518-11-02 00:40] falls asleep
[1518-11-02 00:50] wakes up
[1518-11-03 00:05] Guard #10 begins shift
[1518-11-03 00:24] falls asleep
[1518-11-03 00:29] wakes up
[1518-11-04 00:02] Guard #99 begins shift
[1518-11-04 00:36] falls asleep
[1518-11-04 00:46] wakes up
[1518-11-05 00:03] Guard #99 begins shift
[1518-11-05 00:45] falls asleep
[1518-11-05 00:55] wakes up"

  part_one: 240
  part_two: 4455
}
