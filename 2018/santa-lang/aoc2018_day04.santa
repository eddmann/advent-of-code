input: read("aoc://2018/4")

let tally_guards_sleep = |input| {
  input
    |> lines
    |> sort(>)
    |> reduce_s(
      |[sleep, guard, start], action| {
        if action[25] == "#" {
          return [sleep, int(split(" ", action)[3][1..]), nil];
        }

        if action[25] == "a" {
          return [sleep, guard, int(action[15..17])];
        }

        let sleep_range = start..int(action[15..17]) - 1;

        let updated_sleep = update(
          guard,
          #{},
          |guard_sleep| {
            reduce(
              |hours, hour| update(hour, 0, _ + 1, hours),
              guard_sleep,
              sleep_range
            );
          },
          sleep
        );

        return [updated_sleep, guard, nil];
      },
      [#{}, nil, nil]
    )
    |> map(reduce(|hours, total, hour| hours + [[hour, total]], []))
};

part_one: {
  let guards_sleep = tally_guards_sleep(input);

  let id = guards_sleep
    |> reduce(
      |guards, hours, id| {
        guards + [[id, hours |> reduce(|total, [_, time]| total + time, 0)]];
      },
      []
    )
    |> sort(|a, b| a[1] < b[1])
    |> first
    |> first;

  let minute = guards_sleep[id]
    |> sort(|a, b| a[1] < b[1])
    |> first
    |> first;

  id * minute;
}

part_two: {
  let guards_sleep = tally_guards_sleep(input);

  let id = guards_sleep
    |> reduce(
      |guards, hours, id| {
        guards + [[id, hours |> map(get(1)) |> max]];
      },
      []
    )
    |> sort(|a, b| a[1] < b[1])
    |> first
    |> first;

  let minute = guards_sleep[id]
    |> sort(|a, b| a[1] < b[1])
    |> first
    |> first;

  id * minute;
}

test: {
  input: "[1518-11-01 00:00] Guard #10 begins shift
[1518-11-01 00:05] falls asleep
[1518-11-01 00:25] wakes up
[1518-11-01 00:30] falls asleep
[1518-11-01 00:55] wakes up
[1518-11-01 23:58] Guard #99 begins shift
[1518-11-02 00:40] falls asleep
[1518-11-02 00:50] wakes up
[1518-11-03 00:05] Guard #10 begins shift
[1518-11-03 00:24] falls asleep
[1518-11-03 00:29] wakes up
[1518-11-04 00:02] Guard #99 begins shift
[1518-11-04 00:36] falls asleep
[1518-11-04 00:46] wakes up
[1518-11-05 00:03] Guard #99 begins shift
[1518-11-05 00:45] falls asleep
[1518-11-05 00:55] wakes up"

  part_one: 240
  part_two: 4455
}
