input: read("aoc://2018/4")

let tally_guards_sleep = |input| {
  input
    |> lines
    |> sort(>)
    |> fold_s(
      [#{}, nil, nil],
      |[sleep, guard, start], action| {
        if action[25] == "#" {
          return [sleep, int(split(" ", action)[3][1..]), nil];
        }

        if action[25] == "a" {
          return [sleep, guard, int(action[15..17])];
        }

        let sleep_range = start..int(action[15..17]);

        let updated_sleep = sleep |> update_d(
          guard,
          #{},
          |guard_sleep| {
            fold(
              guard_sleep,
              |hours, hour| update_d(hour, 0, _ + 1, hours),
              sleep_range
            );
          }
        );

        [updated_sleep, guard, nil];
      }
    )
    |> map(fold([], |hours, total, hour| hours + [[hour, total]]))
};

part_one: {
  let guards_sleep = tally_guards_sleep(input);

  let id = guards_sleep
    |> fold(
      [],
      |guards, hours, id| {
        guards + [[id, hours |> fold(0, |total, [_, time]| total + time)]];
      }
    )
    |> sort(|[_, total_a], [_, total_b]| total_a < total_b)
    |> |[[id]]| id;

  let minute = guards_sleep[id]
    |> sort(|[_, total_a], [_, total_b]| total_a < total_b)
    |> |[[minute]]| minute;

  id * minute;
}

part_two: {
  let guards_sleep = tally_guards_sleep(input);

  let id = guards_sleep
    |> fold(
      [],
      |guards, hours, id| {
        guards + [[id, hours |> map(get(1)) |> max]];
      }
    )
    |> sort(|[_, total_a], [_, total_b]| total_a < total_b)
    |> |[[id]]| id;

  let minute = guards_sleep[id]
    |> sort(|[_, total_a], [_, total_b]| total_a < total_b)
    |> |[[minute]]| minute

  id * minute;
}

test: {
  input: "[1518-11-01 00:00] Guard #10 begins shift
[1518-11-01 00:05] falls asleep
[1518-11-01 00:25] wakes up
[1518-11-01 00:30] falls asleep
[1518-11-01 00:55] wakes up
[1518-11-01 23:58] Guard #99 begins shift
[1518-11-02 00:40] falls asleep
[1518-11-02 00:50] wakes up
[1518-11-03 00:05] Guard #10 begins shift
[1518-11-03 00:24] falls asleep
[1518-11-03 00:29] wakes up
[1518-11-04 00:02] Guard #99 begins shift
[1518-11-04 00:36] falls asleep
[1518-11-04 00:46] wakes up
[1518-11-05 00:03] Guard #99 begins shift
[1518-11-05 00:45] falls asleep
[1518-11-05 00:55] wakes up"

  part_one: 240
  part_two: 4455
}
