input: read("aoc://2018/8")

let parse_license = split(" ") >> map(int);

part_one: {
  let sum_metadata = |license| {
    let [children, meta, ..rest] = license;

    if (children == 0) {
      return [sum(rest[0..meta]), rest[meta..]];
    }

    let [total, next_rest] = reduce(
      |[total, rest]| {
        let [sub_total, next_rest] = sum_metadata(rest);
        return [total + sub_total, next_rest];
      },
      [0, rest],
      0..children - 1
    );

    [total + sum(next_rest[0..meta]), next_rest[meta..]];
  }

  parse_license(input)
    |> sum_metadata
    |> first
}

part_two: {
  let root_value = |license| {
    let [children, meta, ..rest] = license;

    if (children == 0) {
      return [sum(rest[0..meta]), rest[meta..]];
    }

    let [metadata, next_rest] = reduce(
      |[metadata, rest]| {
        let [sub_metadata, next_rest] = root_value(rest);
        return [[..metadata, sub_metadata], next_rest];
      },
      [[], rest],
      0..children - 1
    );

    let value = reduce(
      |acc, idx| if get(idx - 1, metadata) { acc + get(idx - 1, metadata) } else { acc },
      0,
      next_rest[0..meta]
    );

    [value, next_rest[meta..]];
  }

  parse_license(input)
    |> root_value
    |> first
}

test: {
  input: "2 3 0 3 10 11 12 1 1 0 1 99 2 1 1 2"
  part_one: 138
  part_two: 66
}
