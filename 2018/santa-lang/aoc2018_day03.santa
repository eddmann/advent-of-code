input: read("aoc://2018/3")

let parse_claims =
  lines >> map(|line| {
    let [id, x, y, width, height] = regex_match("#([0-9]+) @ ([0-9]+),([0-9]+): ([0-9]+)x([0-9]+)", line) |> map(int);
    #{id, x, y, width, height};
  });

let compute_squares = |claim|
  (claim["x"]..claim["x"] + claim["width"] - 1)
    |> flat_map(|x| (claim["y"]..claim["y"] + claim["height"] - 1) |> map(|y| [x, y]))
    |> set;

let compute_fabric = reduce(
  |fabric, claim| {
    compute_squares(claim)
      |> reduce(
        |fabric, square| update(square, {}, |ids| ids + {claim["id"]}, fabric),
        fabric
      );
  },
  #{}
);

part_one: {
  parse_claims(input)
    |> compute_fabric
    |> filter(|square| size(square) > 1)
    |> size;
}

part_two: {
  let all = |predicate, collection| collection |> filter(predicate) == collection;

  let claims = parse_claims(input);
  let fabric = compute_fabric(claims);

  claims
    |> find(|claim| compute_squares(claim) |> all(|square| fabric[square] |> size == 1))
    |> get("id");
}

test: {
  input: "#1 @ 1,3: 4x4\n#2 @ 3,1: 4x4\n#3 @ 5,5: 2x2"
  part_one: 4
  part_two: 3
}
