input: read("aoc://2018/1")

let parse_deltas = ints

part_one: {
  parse_deltas(input) |> sum
}

part_two: {
  let scan = |fn, initial, collection| {
    let mut previous = initial;

    map(|value| previous = fn(previous, value), collection)
  }

  let mut seen = {};

  parse_deltas(input)
    |> cycle
    |> scan(+, 0)
    |> find |frequency| {
      if seen `includes?` frequency {
        return true
      }
      seen = seen + {frequency};

      false
    }
}

test: {
  input: "+1\n+1\n+1"
  part_one: 3
}

test: {
  input: "+1\n+1\n-2"
  part_one: 0
}

test: {
  input: "-1\n-2\n-3"
  part_one: -6
}

test: {
  input: "+3
+3
+4
-2
-4"
  part_two: 10
}

test: {
  input: "-6
+3
+8
+5
-6"
  part_two: 5
}

test: {
  input: "+7
+7
-2
-7
-4"
  part_two: 14
}

test: {
  input: read("aoc://2018/1")
  part_one: 474
  part_two: 137041
}
